#!/usr/bin/env zsh

#------------------------------------------------------------------------------
# ZSH Specfic Aliases
#------------------------------------------------------------------------------
# Disable correction.
for com in "ack" "ag" "cd" "cp" "egrep" "fd" "find" "fgrep" "gcc" "gist" "grep" \
  "heroku" "ln" "man" "mkdir" "mv" "mysql" "psql" "rg" "rm"; do
    alias $com="nocorrect ${aliases[$com]:-$com}"
done

# Disable globbing.
for com in "fc" "ftp" "history" "locate" "rake" "rsync" "scp" "sftp"; do
  alias $com="noglob ${aliases[$com]:-$com}"
done

# Safety first.
for com in "cp" "ln" "mv" "rm"; do
  alias $com="${aliases[$com]:-$com} -i"
done

# History
# -------------------------
# No log history.
# Use a space at the beginning to prevent command entering history.
for com in "clear" "exit" "fg" "history" "jobs" "pass" "pwd"; do
  alias $com=" ${aliases[$com]:-$com}";
done

# Lists the ten most used commands.
alias h="history"
alias hstat="history 0 | awk '{print \$2}' | sort | uniq -c | sort -n -r | head"

# Open
# -------------------------
# Check for various OS openers. Quit as soon as we find one that works.
for opener in browser-exec xdg-open cmd.exe cygstart "start" open; do
  if (( $+commands[${opener}] )); then
    if [[ "${opener}" == "cmd.exe" ]]; then
      alias open="${opener} /c start";
    else
      alias open="${opener}";
    fi
    break;
  fi
done

# Path
# -------------------------
# Show PATH info in a more readable format.
alias fpath="echo \${FPATH} | tr ':' '\n' | nl"
alias path="echo \${PATH} | tr ':' '\n' | nl"

# General aliases
# -------------------------
alias _="sudo"
alias a="alias"
alias as="alias | grep -i"
alias afk="${XDG_CONFIG_HOME}/i3/bin/i3-exit lock"
alias b="${(z)BROWSER}"
alias clr='clear && echo "Currently logged in on ${TTY}, as ${LOGNAME} in directory ${PWD}"'
alias df="df -kh"
alias du="du -kh"
alias e="${(z)VISUAL:-${(z)EDITOR}}"
alias g="git"
alias md="mkdir -pv"
alias o="open"
alias p="${(z)PAGER}"
alias r="ranger"
alias type="type -a"
alias v="vi"
alias x="extract"

# Carry over aliases to the root account when using sudo
alias sudo="sudo "

# head and tail will show as much possible without scrolling
alias head="head -n \$((\${LINES:-12}-4))"
alias tail="tail -n \$((\${LINES:-12}-4)) -s.1"

# rsync sensible defaults.
alias rsync="${aliases[rsync]:-rsync} --verbose --archive --info=progress2 --human-readable --compress --partial"

# Alias cat to be bat.
(( $+commands[bat] )) && alias cat=bat

# help - make this tldr.
(( $+commands[tldr] )) && alias help="tldr"

# Hex dump Canonical; some systems have this symlinked
(( $+commands[hd] )) || alias hd="hexdump -C"

# ls
# ls alias is set in colors sh config.
# If ls is set to exa. Some of these aliases will not work.
# TODO: add checks for exa.
if (( $+commands[exa] )); then
  alias ll="ls -l"
  alias la="ll"
  alias lr="ll -R"                     # recursively.
  alias lk="ll -s=size"                # Sorted by size
  alias lm="ll -s=modified"            # Sorted by modified date
  alias lc="ll --created -s=created"   # Sorted by created date
else
  alias ll="ls -lh"        # Lists human readable sizes.
  alias la="ll -A"         # Lists human readable all
  alias lr="ll -R"         # Lists human readable sizes, recursively.
  alias lx="ll -XB"        # Lists sorted by extension (GNU only).
  alias lk="ll -Sr"        # Lists sorted by size, largest last.
  alias lt="ll -tr"        # Lists sorted by date, most recent last.
  alias lc="lt -c"         # Lists sorted by date, most recent last, shows change time.
  alias lu="lt -u"         # Lists sorted by date, most recent last, shows access time.
fi

# process
alias ps="ps -ax"
alias pst="pstree -g 3 -ws"

# Systemd
if (( $+commands[systemctl] )); then
  alias jc="journalctl"
  alias jcu="journalctl --user"
  alias sc="systemctl"
  alias scu="systemctl --user"
fi

# NeoMutt / Mutt
if (( $+commands[neomutt] )); then
  alias mutt="neomutt"
fi

# Neovim / Vim
if (( $+commands[nvim] )); then
  alias suvim="sudo -E nvim"
  alias vi="nvim"
  alias vim="nvim"
  alias vimdiff="nvim -d"
elif (( $+commands[vim] )); then
  alias suvim="sudo -E vim"
  alias vi="vim"
  alias vimdiff="vim -d"
fi
if (( $+commands[fzf] )); then
  alias v="vi \$(fzf)"
fi

# Youtube DL
if (( $+commands[youtube-dl] )); then
  alias yt="youtube-dl --add-metadata --ignore-errors --continue --restrict-filenames" # Download video link
  alias yta="yt --extract-audio --format bestaudio/best" # Download only audio
fi

# File Download
if (( $+commands[curl] )); then
  alias get="curl --continue-at - --location --progress-bar --remote-name --remote-time"
elif (( $+commands[wget] )); then
  alias get="wget --continue --progress=bar --timestamping"
fi

# Network aliases
# -------------------------
# IP addresses
alias pubip="dig +short myip.opendns.com @resolver1.opendns.com"
alias localip="sudo ifconfig | grep -Eo 'inet (addr:)?([0-9]*\\.){3}[0-9]*' | grep -Eo '([0-9]*\\.){3}[0-9]*' | grep -v '127.0.0.1'"
alias ips="sudo ifconfig -a | grep -o 'inet6\\? \\(addr:\\)\\?\\s\\?\\(\\(\\([0-9]\\+\\.\\)\\{3\\}[0-9]\\+\\)\\|[a-fA-F0-9:]\\+\\)' | awk '{ sub(/inet6? (addr:)? ?/, \"\"); print }'"

# See what's going on.
alias httpdump="sudo tcpdump -i en1 -n -s 0 -w - | grep -a -o -E \"Host\: .*|GET \/.*\""
alias sniff="sudo ngrep -d 'en1' -t '^(GET|POST) ' 'tcp and port 80'"

# Serve content. Cater for both python versions.
if (( $+commands[python3] )); then
    alias http-serve="python3 -m http.server"
else
    alias http-serve="python -m SimpleHTTPServer"
fi

# Misc aliases
# -------------------------
# Weather
alias weather="curl -s wttr.in"


# macOS aliases
#-----------------------
if [[ "${KERNEL}" == "Darwin" ]]; then
  # Lock the screen (when going AFK)
  alias afk="/System/Library/CoreServices/Menu\\ Extras/User.menu/Contents/Resources/CGSession -suspend"

  # Recursively delete `.DS_Store` files
  alias cleanup_dsstore="find . -name '*.DS_Store' -type f -ls -delete"

  # Flush the DNS on Mac
  alias dnsflush="sudo dscacheutil -flushcache && sudo killall -HUP mDNSResponder"

  # Empty the Trash on all mounted volumes and the main HDD
  # Also, clear Apple’s System Logs to improve shell startup speed
  alias emptytrash=" \
        sudo rm -rfv /Volumes/*/.Trashes; \
        sudo rm -rfv ~/.Trash; \
        sudo rm -rfv /private/var/log/asl/*.asl; \
        sqlite3 ~/Library/Preferences/com.apple.LaunchServices.sQuarantineEventsV* 'delete from LSQuarantineEvent' \
    "

  # Hide/show all desktop icons (useful when presenting)
  alias hidedesktop="defaults write com.apple.finder CreateDesktop -bool falsel && killall Finder"
  alias showdesktop="defaults write com.apple.finder CreateDesktop -bool true && killall Finder"

  # Clean up LaunchServices to remove duplicates in the “Open With” menu
  alias lscleanup="/System/Library/Frameworks/CoreServices.framework/Frameworks/LaunchServices.framework/Support/lsregister -kill -r -domain local -domain system -domain user && killall Finder"

  # No `md5sum`, so use `md5` as a fallback
  (( $+commands[md5sum] )) || alias md5sum="md5"

  # No `sha1sum`, so use `shasum` as a fallback
  (( $+commands[sha1sum] )) || alias sha1sum="shasum"
fi
