#!/usr/bin/env zsh

#------------------------------------------------------------------------------
# ZSH Options
#------------------------------------------------------------------------------
# Remove the ^S ^Q mappings. See all mappings: stty -a
# Disable start/stop characters in shell editor.
# Only run in an interactive shell
if [[ $- != *i* ]] then
    stty ixany
    stty ixoff -ixon
fi
setopt NO_FLOW_CONTROL

# Basics
# ----------------------
#
# Combine zero-length punctuation characters (accents)
# with the base character.
setopt COMBINING_CHARS

# If you have CORRECT set, the shell will check all the commands you type and
# if they don't exist, but there is one with a similar name, it will ask you
# if you meant that one instead. You can type `n' for no, don't correct, just
# go ahead; `y' for yes, correct it then go ahead; `a' for abort, don't do
# anything; `e' for edit, return to the editor to edit the same line again.
# Users of the new completion system should note this is not the same
# correction you get there: it's just simple correction of commands.
setopt CORRECT

# Enable "=command" feature
setopt EQUALS

# Show a message with the exit code when a command returns with a non-zero exit code
setopt PRINT_EXIT_VALUE

# Don't print a warning message if a mail file has been accessed.
unsetopt MAIL_WARNING

setopt INTERACTIVECOMMENTS   # Ignore lines prefixed with '#'
setopt NO_BEEP               # beeps are annoying
setopt NO_NOMATCH            # Avoid problem with HEAD^
setopt RM_STAR_WAIT          # 10 second wait if you try to delete everything

# History
# ----------------------
export HISTFILE="${XDG_DATA_HOME}/zsh/history"
export HISTSIZE=10000
export SAVEHIST=10000


# Completion
# ----------------------
setopt AUTO_PARAM_KEYS
setopt AUTO_PARAM_SLASH      # If param is a dir, add a trailing slash.
setopt LIST_TYPES            # list like "ls -F"
setopt LIST_PACKED           # Try to make the completion list small
unsetopt COMPLETE_ALIASES    # Completion of cmd switches for aliases
setopt MAGIC_EQUAL_SUBST     # Enable completion in "--option=arg"
setopt MARK_DIRS             # Add "/" if completes directory
setopt PATH_DIRS             # Path search even on cmd bins with slashes.

# Typically, compinit will not automatically find new executables in the
# $PATH. For example, after you install a new package, the files in /usr/bin
# would not be immediately or automatically included in the completion. Thus,
# to have these new executables included, one would run a rehash.
# This 'rehash' can be set to happen automatically as below.
zstyle ':completion:*' rehash true

# Use caching to make completion for commands such as dpkg and apt usable.
zstyle ':completion::complete:*' use-cache on
zstyle ':completion::complete:*' cache-path "${XDG_CACHE_HOME}/zsh"


# Jobs
# ----------------------
setopt LONG_LIST_JOBS     # List jobs in the long format by default.
setopt AUTO_RESUME        # Attempt to resume existing job before creating a new process.
setopt NOTIFY             # Report status of background jobs immediately.
unsetopt BG_NICE          # Do not run all background jobs at a lower priority.
unsetopt HUP              # Do not kill jobs on shell exit.
unsetopt CHECK_JOBS       # Do not report on jobs when shell exit.


# Smart URLS
# ----------------------
# This logic comes from an old version of zim. Essentially, bracketed-paste was
# added as a requirement of url-quote-magic in 5.1, but in 5.1.1 bracketed
# paste had a regression. Additionally, 5.2 added bracketed-paste-url-magic
# which is generally better than url-quote-magic so we load that when possible.
autoload -Uz is-at-least
if [[ "${ZSH_VERSION}" != "5.1.1" ]]; then
    if is-at-least "5.2"; then
        autoload -Uz bracketed-paste-url-magic
        zle -N bracketed-paste bracketed-paste-url-magic
    else
        if is-at-least "5.1"; then
            autoload -Uz bracketed-paste-magic
            zle -N bracketed-paste bracketed-paste-magic
        fi
    fi
    autoload -Uz url-quote-magic
    zle -N self-insert url-quote-magic
fi
