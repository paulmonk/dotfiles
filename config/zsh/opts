#!/usr/bin/env zsh

#------------------------------------------------------------------------------
# ZSH Options
#------------------------------------------------------------------------------
# Remove the ^S ^Q mappings. See all mappings: stty -a
# Disable start/stop characters in shell editor.
stty ixany
stty ixoff -ixon
setopt NO_FLOW_CONTROL

# Basics
# ----------------------
#
# Combine zero-length punctuation characters (accents)
# with the base character.
setopt COMBINING_CHARS

# If you have CORRECT set, the shell will check all the commands you type and
# if they don't exist, but there is one with a similar name, it will ask you
# if you meant that one instead. You can type `n' for no, don't correct, just
# go ahead; `y' for yes, correct it then go ahead; `a' for abort, don't do
# anything; `e' for edit, return to the editor to edit the same line again.
# Users of the new completion system should note this is not the same
# correction you get there: it's just simple correction of commands.
setopt CORRECT

# Enable "=command" feature
setopt EQUALS

# Show a message with the exit code when a command returns with a non-zero exit code
setopt PRINT_EXIT_VALUE

# Don't print a warning message if a mail file has been accessed.
unsetopt MAIL_WARNING

setopt INTERACTIVECOMMENTS   # Ignore lines prefixed with '#'
setopt NO_BEEP               # beeps are annoying
setopt NO_NOMATCH            # Avoid problem with HEAD^
setopt MULTIOS               # Now we can pipe to multiple outputs!
setopt RM_STAR_WAIT          # 10 second wait if you try to delete everything

# Dirs
# ----------------------
setopt AUTO_CD              # Auto changes to a directory without typing cd.
setopt AUTO_PUSHD           # Push the old directory onto the stack on cd.
setopt PUSHD_IGNORE_DUPS    # Do not store duplicates in the stack.
setopt PUSHD_SILENT         # Do not print the directory stack after pushd or popd.
setopt PUSHD_TO_HOME        # Push to home directory when no argument is given.
setopt CDABLE_VARS          # Change directory to a path stored in a variable.
setopt MULTIOS              # Now we can pipe to multiple outputs!
setopt EXTENDED_GLOB        # Use extended globbing syntax.
# Do not overwrite existing files with > and >>. Use >! and >>! to bypass
unsetopt CLOBBER

# History
# ----------------------
setopt BANG_HIST                 # Treat the '!' character specially during expansion.
setopt EXTENDED_HISTORY          # Write the history file in the ':start:elapsed;command' format.
setopt INC_APPEND_HISTORY        # Write to the history file immediately, not when the shell exits.
setopt SHARE_HISTORY             # Share history between all sessions.
setopt HIST_EXPIRE_DUPS_FIRST    # Expire a duplicate event first when trimming history.
setopt HIST_IGNORE_DUPS          # Do not record an event that was just recorded again.
setopt HIST_IGNORE_ALL_DUPS      # Delete an old recorded event if a new event is a duplicate.
setopt HIST_FIND_NO_DUPS         # Do not display a previously found event.
setopt HIST_IGNORE_SPACE         # Do not record an event starting with a space.
setopt HIST_NO_STORE             # Remove history command from the history list.
setopt HIST_SAVE_NO_DUPS         # Do not write a duplicate event to the history file.
setopt HIST_VERIFY               # Do not execute immediately upon history expansion.
export HISTFILE="${XDG_DATA_HOME}/zsh/history"
export HISTSIZE=10000
export SAVEHIST=10000


# Completion
# ----------------------
setopt ALWAYS_TO_END         # Move cursor to the end of a completed word.
setopt AUTO_LIST             # Auto list choices on ambiguous completion.
setopt AUTO_MENU             # Show completion menu on a successive tab press.
setopt AUTO_PARAM_KEYS
setopt AUTO_PARAM_SLASH      # If param is a dir, add a trailing slash.
setopt LIST_TYPES            # list like "ls -F"
setopt LIST_PACKED           # Try to make the completion list small
unsetopt COMPLETE_ALIASES    # Completion of cmd switches for aliases
setopt COMPLETE_IN_WORD      # Complete from both ends of a word.
setopt MAGIC_EQUAL_SUBST     # Enable completion in "--option=arg"
unsetopt MENU_COMPLETE       # Do not autoselect the first completion entry.
setopt MARK_DIRS             # Add "/" if completes directory
setopt PATH_DIRS             # Path search even on cmd bins with slashes.

# Jobs
# ----------------------
setopt LONG_LIST_JOBS     # List jobs in the long format by default.
setopt AUTO_RESUME        # Attempt to resume existing job before creating a new process.
setopt NOTIFY             # Report status of background jobs immediately.
unsetopt BG_NICE          # Do not run all background jobs at a lower priority.
unsetopt HUP              # Do not kill jobs on shell exit.
unsetopt CHECK_JOBS       # Do not report on jobs when shell exit.

# Zstyle
# ----------------------
# Typically, compinit will not automatically find new executables in the
# $PATH. For example, after you install a new package, the files in /usr/bin
# would not be immediately or automatically included in the completion. Thus,
# to have these new exectuables included, one would run a rehash.
# This 'rehash' can be set to happen automatically as below.
zstyle ':completion:*' rehash true

# If you end up using a directory as argument, this will remove the trailing slash (useful in ln)
zstyle ':completion:*' squeeze-slashes true

# Prevent CVS files/directories from being completed:
zstyle ':completion:*:(all-|)files' ignored-patterns '(|*/)CVS'
zstyle ':completion:*:cd:*' ignored-patterns '(*/)#CVS'

# Case-insensitive (all), partial-word, and then substring completion.
zstyle ':completion:*' matcher-list 'm:{a-zA-Z}={A-Za-z}' 'r:|[._-]=* r:|=*' 'l:|=* r:|=*'

# Group matches and describe.
zstyle ':completion:*:*:*:*:*' menu select
zstyle ':completion:*:matches' group 'yes'
zstyle ':completion:*:options' description 'yes'
zstyle ':completion:*:options' auto-description '%d'
zstyle ':completion:*:corrections' format ' %F{green}-- %d (errors: %e) --%f'
zstyle ':completion:*:descriptions' format ' %F{yellow}-- %d --%f'
zstyle ':completion:*:messages' format ' %F{purple} -- %d --%f'
zstyle ':completion:*:warnings' format ' %F{red}-- no matches found --%f'
zstyle ':completion:*:default' list-prompt '%S%M matches%s'
zstyle ':completion:*' format ' %F{yellow}-- %d --%f'
zstyle ':completion:*' group-name ''
zstyle ':completion:*' verbose yes

# Fuzzy match mistyped completions.
zstyle ':completion:*' completer _complete _match _approximate
zstyle ':completion:*:match:*' original only
zstyle ':completion:*:approximate:*' max-errors 1 numeric

# Increase the number of errors based on the length of the typed word. But make
# sure to cap (at 7) the max-errors to avoid hanging.
zstyle -e ':completion:*:approximate:*' max-errors 'reply=($((($#PREFIX+$#SUFFIX)/3>7?7:($#PREFIX+$#SUFFIX)/3))numeric)'

# Don't complete unavailable commands.
zstyle ':completion:*:functions' ignored-patterns '(_*|pre(cmd|exec))'

# Array completion element sorting.
zstyle ':completion:*:*:-subscript-:*' tag-order indexes parameters

# Directories
zstyle ':completion:*:default' list-colors ${(s.:.)LS_COLORS}
zstyle ':completion:*:*:cd:*' tag-order local-directories directory-stack path-directories
zstyle ':completion:*:*:cd:*:directory-stack' menu yes select
zstyle ':completion:*:-tilde-:*' group-order 'named-directories' 'path-directories' 'users' 'expand'
zstyle ':completion:*' squeeze-slashes true

# History
zstyle ':completion:*:history-words' stop yes
zstyle ':completion:*:history-words' remove-all-dups yes
zstyle ':completion:*:history-words' list false
zstyle ':completion:*:history-words' menu yes

# Environmental Variables
zstyle ':completion::*:(-command-|export):*' fake-parameters ${${${_comps[(I)-value-*]#*,}%%,*}:#-*-}

# Don't complete uninteresting users...
zstyle ':completion:*:*:*:users' ignored-patterns \
  adm amanda apache avahi beaglidx bin cacti canna clamav daemon \
  dbus distcache dovecot fax ftp games gdm gkrellmd gopher \
  hacluster haldaemon halt hsqldb ident junkbust ldap lp mail \
  mailman mailnull mldonkey mysql nagios \
  named netdump news nfsnobody nobody nscd ntp nut nx openvpn \
  operator pcap postfix postgres privoxy pulse pvm quagga radvd \
  rpc rpcuser rpm shutdown squid sshd sync uucp vcsa xfs '_*'

# ... unless we really want to.
zstyle '*' single-ignored show

# Ignore multiple entries.
zstyle ':completion:*:(rm|kill|diff):*' ignore-line other
zstyle ':completion:*:rm:*' file-patterns '*:all-files'

# Kill
zstyle ':completion:*:*:*:*:processes' command 'ps -u $LOGNAME -o pid,user,command -w'
zstyle ':completion:*:*:kill:*:processes' list-colors '=(#b) #([0-9]#) ([0-9a-z-]#)*=01;36=0=01'
zstyle ':completion:*:*:kill:*' menu yes select
zstyle ':completion:*:*:kill:*' force-list always
zstyle ':completion:*:*:kill:*' insert-ids single

# Man
zstyle ':completion:*:manuals' separate-sections true
zstyle ':completion:*:manuals.(^1*)' insert-sections true

# Media Players
zstyle ':completion:*:*:mpg123:*' file-patterns '*.(mp3|MP3):mp3\ files *(-/):directories'
zstyle ':completion:*:*:mpg321:*' file-patterns '*.(mp3|MP3):mp3\ files *(-/):directories'
zstyle ':completion:*:*:ogg123:*' file-patterns '*.(ogg|OGG|flac):ogg\ files *(-/):directories'
zstyle ':completion:*:*:mocp:*' file-patterns '*.(wav|WAV|mp3|MP3|ogg|OGG|flac):ogg\ files *(-/):directories'

# Smart URLS
# ----------------------
# This logic comes from an old version of zim. Essentially, bracketed-paste was
# added as a requirement of url-quote-magic in 5.1, but in 5.1.1 bracketed
# paste had a regression. Additionally, 5.2 added bracketed-paste-url-magic
# which is generally better than url-quote-magic so we load that when possible.
autoload -Uz is-at-least
if [[ "${ZSH_VERSION}" != "5.1.1" ]]; then
  if is-at-least "5.2"; then
    autoload -Uz bracketed-paste-url-magic
    zle -N bracketed-paste bracketed-paste-url-magic
  else
    if is-at-least "5.1"; then
      autoload -Uz bracketed-paste-magic
      zle -N bracketed-paste bracketed-paste-magic
    fi
  fi
  autoload -Uz url-quote-magic
  zle -N self-insert url-quote-magic
fi
