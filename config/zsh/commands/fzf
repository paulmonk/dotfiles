#!/usr/bin/env zsh

#-------------------------------------------
# FZF
#-------------------------------------------

# Defaults
#-------------------------
if [[ -s "/usr/share/fzf/key-bindings.zsh" ]]; then
  source "/usr/share/fzf/key-bindings.zsh"
fi
if [[ -s "/usr/share/fzf/completion.zsh" ]]; then
  source "/usr/share/fzf/completion.zsh"
fi

# General
#-------------------------
# Default Command
if (( $+commands[rg] )); then
  export FZF_DEFAULT_COMMAND="rg --hidden --files --smart-case --follow "
elif (( $+commands[ag] )); then
  export FZF_DEFAULT_COMMAND="ag --hidden --follow --nogroup -g "
fi

# Default Opts
# Solarized Dark High Contrast color scheme
export FZF_DEFAULT_OPTS="
  --color=bg+:#073642,bg:-1,spinner:#2aa198,hl:#268bd2
  --color=fg:#839496,header:#93a1a1,info:#cb4b16,pointer:#2aa198
  --color=marker:#2aa198,fg+:#fdf6e3,prompt:#2aa198,hl+:#2aa198
  --history ${XDG_DATA_HOME}/fzf/history
  --reverse --height 50% --multi
"

# Alt C:
if (( $+commands[fd] )); then
  export FZF_ALT_C_COMMAND="fd --type d --hidden --follow "
fi
export FZF_ALT_C_OPTS="--select-1 --exit-0"

# Ctrl T:
# Automatically select if only 1 and exit if list is empty.
export FZF_CTRL_T_COMMAND="${FZF_DEFAULT_COMMAND}"
export FZF_CTRL_T_OPTS="--select-1 --exit-0"

# Keybindings
#-------------------------
# Use fzf for history if available
fzf-history-widget-accept() {
  fzf-history-widget
  zle accept-line
}
zle -N fzf-history-widget-accept
bindkey '^X^R' fzf-history-widget-accept

# Completion
#-------------------------
if (( $+commands[fd] )); then
  # Use fd (https://github.com/sharkdp/fd) instead of the default find
  # command for listing path candidates.
  # - The first argument to the function ($1) is the base path to start traversal
  # - See the source code (completion.{bash,zsh}) for the details.
  _fzf_compgen_path() {
    fd --hidden --follow . "${1}"
  }

  # Use fd to generate the list for directory completion
  _fzf_compgen_dir() {
    fd --type d --hidden --follow . "${1}"
  }
fi

# Use Ctrl+T to complete with FZF
export FZF_COMPLETION_TRIGGER=""
bindkey '^T' fzf-completion
# bindkey '^I' ${fzf_default_completion}

# Tmux integration.
#-------------------------
if (( $+commands[tmux] )); then
  # fe [FUZZY PATTERN] - Open the selected file with the default editor
  fe() {
    local query file

    query="${1}"
    file=$(fzf-tmux --query="${query}" --select-1 --exit-0)
    [ -n "${file}" ] && "${EDITOR}" "${file}"
  }

  # fec [FUZZY PATTERN] - Open the selected file in VS Code
  fec() {
    local query file

    query="${1}"
    file=$(fzf-tmux --query="${query}" --select-1 --exit-0)
    [ -n "${file}" ] && code "${file}"
  }

  # fda - including hidden directories
  fda() {
    local dir

    dir=$(find "${1:-.}" -type d 2>/dev/null | fzf-tmux +m) && cd "${dir}"
  }

  # cdf - cd into the directory of the selected file
  cdf() {
    local file dir

    file=$(fzf-tmux +m -q "${1}") && dir=$(dirname "${file}") && cd "${dir}"
  }

  # fkill - kill process
  fkill() {
    ps -ef | sed 1d | fzf-tmux -m | awk '{print $2}' | xargs kill -${1:-9}
  }

  # ftpane - switch pane
  ftpane() {
    local panes current_window current_pane target target_window target_pane

    panes="$(tmux list-panes -s -F '#I:#P - #{pane_current_path} #{pane_current_command}')"
    current_pane="$(tmux display-message -p '#I:#P')"
    current_window="$(tmux display-message -p '#I')"

    target=$(echo "${panes}" | grep -v "${current_pane}" | fzf +m --reverse) || return

    target_window=$(echo "${target}" | awk 'BEGIN{FS=":|-"} {print $1}')
    target_pane=$(echo "${target}" | awk 'BEGIN{FS=":|-"} {print $2}' | cut -c 1)

    if [[ "${current_window}" == "${target_window}" ]]; then
      tmux select-pane -t "${target_window}.${target_pane}"
    else
      tmux select-pane -t "${target_window}.${target_pane}" &&
        tmux select-window -t "${target_window}"
    fi
  }

  # fts [FUZZY PATTERN] - Select selected tmux session
  #   - Bypass fuzzy finder if there's only one match (--select-1)
  #   - Exit if there's no match (--exit-0)
  fts() {
    local session query

    query="${1}"
    session=$(tmux list-sessions -F "#{session_name}" | fzf-tmux --query="${query}" --select-1 --exit-0) || return

    tmux switch-client -t "${session}"
  }
fi
