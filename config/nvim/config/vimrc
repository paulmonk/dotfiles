" Heavily borrowed from: https://github.com/rafi/vim-config
" Thanks.

" Runtime and Plugins
" -------------------------------------------------

" Vim is based on Vi. Setting `nocompatible` switches from the default
" Vi-compatibility mode and enables useful Vim functionality. This
" configuration option turns out not to be necessary for the file named
" '~/.vimrc', because Vim automatically enters nocompatible mode if that file
" is present. But we're including it here just in case this config file is
" loaded some other way (e.g. saved as `foo`, and then Vim started with
" `vim -u foo`).
if &compatible
  " vint: -ProhibitSetNoCompatible
  set nocompatible
  " vint: +ProhibitSetNoCompatible
endif

" Set main configuration directory, and where cache/data is stored.
let $VIM_PATH = fnamemodify(resolve(expand('<sfile>:p')), ':h:h')

function! s:source_file(path, ...) abort
  " Source user configuration files with set/global sensitivity
  let use_global = get(a:000, 0, ! has('vim_starting'))
  let abspath = resolve($VIM_PATH . '/' . a:path)
  if ! use_global
    execute 'source' fnameescape(abspath)
    return
  endif

  let tempfile = tempname()
  let content = map(readfile(abspath),
    \ "substitute(v:val, '^\\W*\\zsset\\ze\\W', 'setglobal', '')")
  try
    call writefile(content, tempfile)
    execute printf('source %s', fnameescape(tempfile))
  finally
    if filereadable(tempfile)
      call delete(tempfile)
    endif
  endtry
endfunction

" Initialize startup settings
if has('vim_starting')
  " Use spacebar as leader and ; as secondary-leader
  " Required before loading plugins!
  let g:mapleader="\<Space>"
  let g:maplocalleader=';'

  " Release keymappings prefixes, evict entirely for use of plug-ins.
  nnoremap <Space>  <Nop>
  xnoremap <Space>  <Nop>
  nnoremap ,        <Nop>
  xnoremap ,        <Nop>
  nnoremap ;        <Nop>
  xnoremap ;        <Nop>
  nnoremap m        <Nop>
  xnoremap m        <Nop>
endif

" Load user scripts with confidential information
" or pre-settings like g:elite_modef
if filereadable($VIM_PATH . '/.vault.vim')
  call s:source_file('.vault.vim')
endif

" Initialize plugin-manager and load main config files
call s:source_file('config/init.vim')

" Loading configuration modules
call s:source_file('config/general.vim')
call s:source_file('config/filetype.vim')
call s:source_file('config/mappings.vim')
call s:source_file('config/theme.vim')

" Load user custom local settings
if filereadable($VIM_PATH . 'config/local.vim')
  call s:source_file('config/local.vim')
endif

set secure

" vim: set ts=2 sw=2 tw=80 noet :
